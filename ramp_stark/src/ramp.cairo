// SPDX-License-Identifier: MIT
// Compatible with OpenZeppelin Contracts for Cairo ^2.0.0

// Security contact: mphidel@gmail.com
#[starknet::contract]
pub mod RampStark {
    use openzeppelin::access::ownable::ownable::OwnableComponent;
    use openzeppelin::security::pausable::PausableComponent;
    use openzeppelin::upgrades::interface::IUpgradeable;
    use openzeppelin::upgrades::UpgradeableComponent;
    use openzeppelin::token::erc20::interface::{IERC20Dispatcher, IERC20DispatcherTrait};
    use starknet::{ClassHash, ContractAddress, get_caller_address, get_contract_address};
    use starknet::storage::{
        Map,
        StorageMapReadAccess,
        StorageMapWriteAccess,
        StoragePointerReadAccess,
        StoragePointerWriteAccess
    };
    //use starknet::contract_address_const;
    use ramp_stark::interfaces::ramp_interface::{IRampStack, OnrampMedium, Region};
    use ramp_stark::errors::RampErrors;

    component!(path: PausableComponent, storage: pausable, event: PausableEvent);
    component!(path: OwnableComponent, storage: ownable, event: OwnableEvent);
    component!(path: UpgradeableComponent, storage: upgradeable, event: UpgradeableEvent);

    // External
    #[abi(embed_v0)]
    impl PausableImpl = PausableComponent::PausableImpl<ContractState>;
    #[abi(embed_v0)]
    impl OwnableMixinImpl = OwnableComponent::OwnableMixinImpl<ContractState>;

    // Internal
    impl PausableInternalImpl = PausableComponent::InternalImpl<ContractState>;
    impl OwnableInternalImpl = OwnableComponent::InternalImpl<ContractState>;
    impl UpgradeableInternalImpl = UpgradeableComponent::InternalImpl<ContractState>;

    #[derive(Drop, Serde, Copy)]
    struct AssetInfo {
        fee_percentage: u8,
        revenue_per_asset: u128
    }

    #[storage]
    struct Storage {
    
        // stores booleans for allowed asstes
        allowed_tokens: Map<ContractAddress, bool>,
    
        // tracks the number of allowed assets
        allowed_tokens_tracker: u8,
    
        // stores all allowed assets
        allowed_tokens_map: Map<u8, ContractAddress>,
    
        // Project Revenue per Asset
        // This mapping keeps track of the revenue generated by each asset for the project.
        project_revenue_per_asset: Map<ContractAddress, u256>,

        // Fee charged on each asset deposited
        fee_per_asset: Map<ContractAddress, u8>,

        // Vault Address
        // This is the address of the vault where the project revenue is stored.
        vault_address: ContractAddress,

        // Fee Percentage
        // This variable holds the percentage of the fee charged for each transaction.
        asset_fee_percentage: Map<ContractAddress, u256>,
        #[substorage(v0)]
        #[starknet::storage_node]
        pausable: PausableComponent::Storage,
        #[substorage(v0)]
        #[starknet::storage_node]
        ownable: OwnableComponent::Storage,
        #[substorage(v0)]
        #[starknet::storage_node]
        upgradeable: UpgradeableComponent::Storage,
    }

    #[event]
    #[derive(Drop, starknet::Event)]
    pub enum Event {
        #[flat]
        PausableEvent: PausableComponent::Event,
        #[flat]
        OwnableEvent: OwnableComponent::Event,
        #[flat]
        UpgradeableEvent: UpgradeableComponent::Event,
        AssetAllowedAdded: AssetAllowedAdded,
        AssetAllowedRemoved: AssetAllowedRemoved,
        RampDeposit: RampDeposit,
        RampWithdraw: RampWithdraw,
        VaultChanged: VaultChanged,
        AssetFeeChanged: AssetFeeChanged,
        RevenueWithdrawn: RevenueWithdrawn,
    }

    // Event emitted when an asset is added to the allowed assets list
    #[derive(Drop, PartialEq, starknet::Event)]
    pub struct AssetAllowedAdded {
        #[key]
        pub asset: ContractAddress,
        #[key]
        pub funder: ContractAddress,
        pub initial_fee_percentage: u8,
        pub initial_blalnce: u256
    }

    // Event emitted when an asset is removed from the allowed assets list
    #[derive(Drop, PartialEq, starknet::Event)]
    pub struct AssetAllowedRemoved {
        #[key]
        pub asset: ContractAddress,
        #[key]
        pub balance_recipient: ContractAddress,
        pub balance: u256,
    }

    // Event emitted when a deposit is made to the Ramp protocol
    #[derive(Drop, PartialEq, starknet::Event)]
    pub struct RampDeposit {
        #[key]
        asset: ContractAddress,
        amount: u256,
        #[key]
        sender: ContractAddress,
        medium: OnrampMedium,
        region: Region,
        data: felt252

    }

    // Event emitted when a withdrawal is made from the Ramp protocol
    #[derive(Drop, PartialEq, starknet::Event)]
    pub struct RampWithdraw {
        #[key]
        asset: ContractAddress,
        amount: u256,
        #[key]
        recipient: ContractAddress,
    }

    // Event emitted when the vault address is changed
    #[derive(Drop, PartialEq, starknet::Event)]
    pub struct VaultChanged {
        #[key]
        pub old_vault: ContractAddress,
        #[key]
        pub new_vault: ContractAddress,
    }

    // event emitted when the fee to an asset gets changed
    #[derive(Drop, PartialEq, starknet::Event)]
    pub struct AssetFeeChanged {
        #[key]
        pub asset: ContractAddress,
        pub old_fee: u8,
        #[key]
        pub new_fee: u8
    }

    //event emitted when an asset's revenue gets sent to the vault
    #[derive(Drop, PartialEq, starknet::Event)]
    pub struct RevenueWithdrawn {
        #[key]
        asset: ContractAddress,
        #[key]
        amount: u256,
    }



    #[constructor]
    fn constructor(ref self: ContractState, owner: ContractAddress, vault: ContractAddress) {
        self.ownable.initializer(owner);
        self.vault_address.write(vault);
    }

    #[generate_trait]
    #[abi(per_item)]
    impl ExternalImpl of ExternalTrait {
        
        /// @dev Pause function.
        /// This function is used to pause the contract, preventing any state changes.
        /// Only the Controller can call this function.
        #[external(v0)]
        fn pause(ref self: ContractState) {
            self.ownable.assert_only_owner();
            self.pausable.pause();
        }
    
        /// @dev Unpause function.
        /// This function is used to unpause the contract, allowing state changes again.
        /// Only the Controller can call this function.
        #[external(v0)]
        fn unpause(ref self: ContractState) {
            self.ownable.assert_only_owner();
            self.pausable.unpause();
        }
    }

    #[abi(embed_v0)]
    impl RampStack of IRampStack<ContractState> {

        /// 
        /// @dev Add an asset to the allowed assets list.
        /// @param asset The address of the asset to be added.
        /// @notice This function can only be called by the owner of the contract.
        /// @param funder The address of the intiall balancefunder.
        /// @param fee_percentage The fee percentage for the asset.
        /// @notice This function can only be called by the owner of the contract.
        /// @notice This function also transfers funds to the contract
        /// @notice This function emits the AssetAllowedAdded event to indicate that the asset has been added.
        /// 
        fn add_allowed_asset(
            ref self: ContractState,
            asset: ContractAddress,
            funder: ContractAddress,
            fee_percentage: u8
        ) {
            self.ownable.assert_only_owner();
            // Implementation for adding an asset
            let allowed_assets = self.allowed_tokens.read(asset);
            assert(!allowed_assets, RampErrors::ASSET_ALREADY_ADDED);

            assert(fee_percentage > 0 && fee_percentage < 5, RampErrors::INVALID_FEE_PERCENTAGE);

            let token = IERC20Dispatcher{ contract_address: asset };

            let allowance = token.allowance(funder, get_contract_address());
            if (allowance > 0) {
                token.transfer_from(funder, get_contract_address(), allowance);
            }

            // ipdate asset tracking info
            let tracked_asset_index:u8 = self.allowed_tokens_tracker.read() + 1;
            self.allowed_tokens_tracker.write(tracked_asset_index);

            //update the asset in the tracker
            self.allowed_tokens_map.write(tracked_asset_index, asset);
            self.fee_per_asset.write(asset, fee_percentage);
            self.allowed_tokens.write(asset, true);

            self.emit(AssetAllowedAdded {
                asset,
                funder,
                initial_fee_percentage: fee_percentage,
                initial_blalnce: allowance
            });

        }

        /// 
        /// @dev Remove an asset from the allowed assets list.
        /// @param asset The address of the asset to be removed.
        /// @notice This function can only be called by the owner of the contract.
        /// @param token_receiver The address of the recipient of the asset balance.
        /// @notice This function can only be called by the owner of the contract.
        /// @notice This function transfers the balance of the asset to the token_receiver.
        /// @notice This function emits the AssetAllowedRemoved event to indicate that the asset has been removed.
        /// 
        fn remove_allowed_asset(ref self: ContractState, asset: ContractAddress, token_receiver: ContractAddress) {
            self.ownable.assert_only_owner();
            // Implementation for removing an asset
            assert(self.is_asset_allowed(asset), RampErrors::INVALID_ASSET);

            self.allowed_tokens.write(asset, false);
            let token = IERC20Dispatcher { contract_address: asset };

            // transfer the balance in the contract to the recever
            let balance = token.balance_of(get_contract_address());

            if (balance > 0) {
                token.transfer(token_receiver, balance);
            } 
            let assets = self.allowed_tokens_tracker.read();

            // remove the asset from the tracking map and update the tracking
            for i in 1..assets {
                if self.allowed_tokens_map.read(i) == asset {
                    let last_asset =  self.allowed_tokens_map.read(assets);
                    self.allowed_tokens_map.write(i, last_asset);
                    self.allowed_tokens_tracker.write(assets - 1);
                    break;
                }
            }
            self.emit(AssetAllowedRemoved {
                asset: asset,
                balance_recipient: token_receiver,
                balance: balance
            });

        }
        /// 
        /// @dev function on_ramp_deposit(ContractAddress, uint256, address, OnrampMedium, Region, bytes)
        /// @dev Deposit function for the Ramp protocol.
        /// @param asset The address of the asset to be deposited.
        /// @param amount The amount of the asset to be deposited.
        /// @param sender The address of the sender.
        /// @param medium The medium of the deposit.
        /// @param region The region of the deposit.
        /// @param data The data of the deposit.
        /// @notice This function can only be called when the contract is not paused.
        /// @notice This function transfers the amount of the asset from the sender to the contract.
        /// @notice This function emits the RampDeposit event to indicate that the asset amount
        /// has been deposited from the user to the contract.
        /// 
        fn on_ramp_deposit(
            ref self: ContractState,
            asset: ContractAddress,
            amount: u256,
            sender: ContractAddress,
            medium: OnrampMedium,
            region: Region,
            data: felt252
        ) {
            assert(self.is_asset_allowed(asset), RampErrors::INVALID_ASSET);

            let token = IERC20Dispatcher { contract_address: asset };

            assert(
                token.transfer_from(sender, get_contract_address(), amount),
                RampErrors::TOKEN_DEPOSIT_FAILED
            );

            let fee_percentage: u256 = self.fee_per_asset.read(asset).into();

            let revenue = self.project_revenue_per_asset.read(asset);

            let fee = (fee_percentage * amount) / 100 ;
            self.project_revenue_per_asset.write(asset, revenue + fee);

            self.emit(
                RampDeposit {
                    asset: asset,
                    amount: amount - fee,
                    sender: get_caller_address(),
                    medium,
                    region,
                    data
                }
            );
        }

        /// 
        /// @dev function off_ramp_withdraw(ContractAddress, u256, ContractAddress)
        /// @dev Withdraw function for the Ramp protocol.
        /// @param asset The address of the asset to be withdrawn.
        /// @param amount The amount of the asset to be withdrawn.
        /// @param recipient The address of the recipient.
        /// @notice This function can only be called when the contract is not paused.
        /// @notice This function transfers the amount of the asset from the contract to the recipient.
        /// @notice This function emits the RampWithdraw event to indicate that the asset 
        /// amount has been sent to the user.
        /// @notice This function does not update the revenue per asset for the asset since this gets computed offchain.
        /// 
        fn off_ramp_withdraw(
            ref self: ContractState,
            asset: ContractAddress,
            amount: u256,
            recipient: ContractAddress
        ) {
            assert(self.is_asset_allowed(asset), RampErrors::INVALID_ASSET);
            self.ownable.assert_only_owner();
            let token = IERC20Dispatcher { contract_address: asset };

            let revenue_in_contract: u256 = self.project_revenue_per_asset.read(asset);  
            assert(
                (token.balance_of(get_contract_address()) - revenue_in_contract) > amount,
                RampErrors::INVALID_ASSET_BALANCE
            );

            assert(
                token.transfer(recipient, amount),
                RampErrors::TOKEN_DEPOSIT_FAILED
            );

            self.emit(
                RampWithdraw {
                    asset: asset,
                    amount: amount,
                    recipient: recipient
                }
            );

        }
        
        ///
        /// @dev function withdraw_asset_revenue(ContractAddress) 
        /// @dev withdraws asset revenue from the contract.
        /// @param asset The address of the asset to be withdrawn.
        /// @notice This function can only be called by the owner of the contract.
        /// @notice This function transfers the amount of the asset from the contract to the vault.
        /// @notice This function emits the AssetRevenueWithdrawn event to indicate that the asset balance in 
        /// the contract has been withdrawn.
        /// 
        fn withdraw_asset_revenue(ref self: ContractState, asset: ContractAddress) {
            self.ownable.assert_only_owner();

            let token = IERC20Dispatcher {
                contract_address: asset,
            };

            let revenue = self.project_revenue_per_asset.read(asset);

            let vault_address = self.vault_address.read();

            if (revenue > 0) {
                assert(token.transfer(vault_address, revenue), RampErrors::TOKEN_TRANSFER_ERROR);

            };
            self.emit(
                RevenueWithdrawn {
                    asset: asset,
                    amount: revenue
                }
            );
        }

        /// 
        /// @dev function get_asset_revenue(ContractAddress) 
        /// @dev gets the revenue of an asset.
        /// @param asset The address of the asset to be checked.
        /// @return u256 Returns the revenue of the asset.
        /// 
        fn get_asset_revenue(self: @ContractState, asset: ContractAddress) -> u256 {
            self.project_revenue_per_asset.read(asset)
        }
    
        /// 
        /// @dev function get_asset_fee_percentage(ContractAddress) 
        /// @dev gets the fee percentage of an asset.
        /// @param asset The address of the asset to be checked.
        /// @return u8 Returns the fee percentage of the asset.
        /// 
        fn get_asset_fee_percentage(self: @ContractState, asset: ContractAddress) -> u8 {
            self.fee_per_asset.read(asset)
        }

        ///
        /// @dev function vault()
        /// Gets the vault address
        /// @return ContractAddress
        fn vault(self: @ContractState) -> ContractAddress {
            self.vault_address.read()
        }


        /// 
        /// @dev function is_asset_allowed(ContractAddress) 
        /// @dev checks if an asset is allowed.
        /// @param asset The address of the asset to be checked.
        /// @return bool Returns true if the asset is allowed, false otherwise.
        /// 
        fn is_asset_allowed(self: @ContractState, asset: ContractAddress) -> bool{
            self.allowed_tokens.read(asset)
        }

        ///
        /// @dev function get_allowed_assets() 
        /// @dev gets the list of allowed assets.
        /// @return Array<ContractAddress> Returns an array of addresses of allowed assets.
        /// 
        fn get_allowed_assets(self: @ContractState) -> Array<ContractAddress> {
            let assets = self.allowed_tokens_tracker.read();

            let mut assets_array: Array<ContractAddress> = ArrayTrait::new();

            for i in 1..assets {
                assets_array.append(self.allowed_tokens_map.read(i));
            }
            assets_array
        }

        ///
        /// @dev function set_new_vault(ContractAddress) 
        /// @dev sets a new vault address.
        /// @param _vault The address of the new vault.
        /// @notice This function can only be called by the owner of the contract.
        ///
        fn set_new_vault(ref self: ContractState, vault: ContractAddress) {
            self.ownable.assert_only_owner();

            let old_vault = self.vault_address.read();
            assert(old_vault != vault, RampErrors::SAME_VAULT_ADDRESS);

            self.vault_address.write(vault);

            self.emit(
                VaultChanged {
                    old_vault: old_vault,
                    new_vault: vault
                }
            );
        }

        /// 
        /// @dev function set_fee(ContractAddress, u256)
        /// @dev sets a new fee percentage for an asset.
        /// @param asset The address of the asset.
        /// @param fee The new fee percentage.
        /// @notice This function can only be called by the owner of the contract.
        /// 
        fn set_fee(ref self: ContractState, asset: ContractAddress, fee: u8) {
            self.ownable.assert_only_owner();
            let old_fee = self.fee_per_asset.read(asset);
            assert(
                self.allowed_tokens.read(asset),
                RampErrors::INVALID_ASSET
            );
            assert(
                old_fee != fee,
                RampErrors::SAME_FEE_AMOUNT
            );
            self.fee_per_asset.write(asset, fee);

            self.emit(
                AssetFeeChanged {
                    asset: asset,
                    old_fee: old_fee,
                    new_fee: fee
                }
            );
        }

    }

    //
    // Upgradeable
    //
    #[abi(embed_v0)]
    impl UpgradeableImpl of IUpgradeable<ContractState> {
        fn upgrade(ref self: ContractState, new_class_hash: ClassHash) {
            self.ownable.assert_only_owner();
            self.upgradeable.upgrade(new_class_hash);
        }
    }
}
