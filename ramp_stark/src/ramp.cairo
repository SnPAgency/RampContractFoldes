// SPDX-License-Identifier: MIT
// Compatible with OpenZeppelin Contracts for Cairo ^2.0.0

// Security contact: mphidel@gmail.com
#[starknet::contract]
pub mod RampStark {
    use openzeppelin::access::ownable::ownable::OwnableComponent;
    use openzeppelin::security::pausable::PausableComponent;
    use openzeppelin::upgrades::interface::IUpgradeable;
    use openzeppelin::upgrades::UpgradeableComponent;
    use openzeppelin::token::erc20::interface::{IERC20Dispatcher, IERC20DispatcherTrait};
    use starknet::{ClassHash, ContractAddress, get_caller_address, get_contract_address};
    use starknet::storage::{
        Map,
        StorageMapReadAccess,
        StorageMapWriteAccess,
        StoragePointerReadAccess,
        StoragePointerWriteAccess
    };
    //use starknet::contract_address_const;
    use ramp_stark::interfaces::ramp_interface::IRampStack;
    use ramp_stark::errors::RampErrors;

    component!(path: PausableComponent, storage: pausable, event: PausableEvent);
    component!(path: OwnableComponent, storage: ownable, event: OwnableEvent);
    component!(path: UpgradeableComponent, storage: upgradeable, event: UpgradeableEvent);

    // External
    #[abi(embed_v0)]
    impl PausableImpl = PausableComponent::PausableImpl<ContractState>;
    #[abi(embed_v0)]
    impl OwnableMixinImpl = OwnableComponent::OwnableMixinImpl<ContractState>;

    // Internal
    impl PausableInternalImpl = PausableComponent::InternalImpl<ContractState>;
    impl OwnableInternalImpl = OwnableComponent::InternalImpl<ContractState>;
    impl UpgradeableInternalImpl = UpgradeableComponent::InternalImpl<ContractState>;

    #[storage]
    struct Storage {
    
        // stores booleans for allowed asstes
        allowed_tokens: Map<ContractAddress, bool>,
    
        // tracks the number of allowed assets
        allowed_tokens_tracker: u8,
    
        // stores all allowed assets
        allowed_tokens_map: Map<u8, ContractAddress>,
    
        // Project Revenue per Asset
        // This mapping keeps track of the revenue generated by each asset for the project.
        project_revenue_per_asset: Map<ContractAddress, u256>,

        // Vault Address
        // This is the address of the vault where the project revenue is stored.
        vault_address: ContractAddress,

        // Fee Percentage
        // This variable holds the percentage of the fee charged for each transaction.
        asset_fee_percentage: Map<ContractAddress, u256>,
        #[substorage(v0)]
        #[starknet::storage_node]
        pausable: PausableComponent::Storage,
        #[substorage(v0)]
        #[starknet::storage_node]
        ownable: OwnableComponent::Storage,
        #[substorage(v0)]
        #[starknet::storage_node]
        upgradeable: UpgradeableComponent::Storage,
    }

    #[event]
    #[derive(Drop, starknet::Event)]
    enum Event {
        #[flat]
        PausableEvent: PausableComponent::Event,
        #[flat]
        OwnableEvent: OwnableComponent::Event,
        #[flat]
        UpgradeableEvent: UpgradeableComponent::Event,
        AssetAllowedAdded: AssetAllowedAdded,
        AssetAllowedRemoved: AssetAllowedRemoved,
        RampDeposit: RampDeposit,
        RampWithdraw: RampWithdraw,
        VaultChanged: VaultChanged,
        AssetFeeChanged: AssetFeeChanged
    }

    // Event emitted when an asset is added to the allowed assets list
    #[derive(Drop, PartialEq, starknet::Event)]
    pub struct AssetAllowedAdded {
        #[key]
        pub asset: ContractAddress,
    }

    // Event emitted when an asset is removed from the allowed assets list
    #[derive(Drop, PartialEq, starknet::Event)]
    pub struct AssetAllowedRemoved {
        #[key]
        pub asset: ContractAddress,
    }

    // Event emitted when a deposit is made to the Ramp protocol
    #[derive(Drop, PartialEq, starknet::Event)]
    pub struct RampDeposit {
        #[key]
        asset: ContractAddress,
        amount: u256,
        #[key]
        sender: ContractAddress,
    }

    // Event emitted when a withdrawal is made from the Ramp protocol
    #[derive(Drop, PartialEq, starknet::Event)]
    pub struct RampWithdraw {
        #[key]
        asset: ContractAddress,
        amount: u256,
        #[key]
        recipient: ContractAddress,
    }

    // Event emitted when the vault address is changed
    #[derive(Drop, PartialEq, starknet::Event)]
    pub struct VaultChanged {
        #[key]
        old_vault: ContractAddress,
        #[key]
        new_vault: ContractAddress,
    }

    // event emitted when the fee to an asset gets changed
    #[derive(Drop, PartialEq, starknet::Event)]
    pub struct AssetFeeChanged {
        #[key]
        asset: ContractAddress,
        old_fee: u256,
        #[key]
        new_fee: u256
    }



    #[constructor]
    fn constructor(ref self: ContractState, owner: ContractAddress, vault: ContractAddress) {
        self.ownable.initializer(owner);
        self.vault_address.write(vault);
    }

    #[generate_trait]
    #[abi(per_item)]
    impl ExternalImpl of ExternalTrait {
        
        /// @dev Pause function.
        /// This function is used to pause the contract, preventing any state changes.
        /// Only the Controller can call this function.
        #[external(v0)]
        fn pause(ref self: ContractState) {
            self.ownable.assert_only_owner();
            self.pausable.pause();
        }
    
        /// @dev Unpause function.
        /// This function is used to unpause the contract, allowing state changes again.
        /// Only the Controller can call this function.
        #[external(v0)]
        fn unpause(ref self: ContractState) {
            self.ownable.assert_only_owner();
            self.pausable.unpause();
        }
    }

    #[abi(embed_v0)]
    impl RampStack of IRampStack<ContractState> {

        /// 
        /// @dev Add an asset to the allowed assets list.
        /// @param asset The address of the asset to be added.
        /// @notice This function can only be called by the owner of the contract.
        /// 
        fn add_allowed_asset(ref self: ContractState, asset: ContractAddress) {
            self.ownable.assert_only_owner();
            // Implementation for adding an asset
            let allowed_assets = self.allowed_tokens.read(asset);
            assert(!allowed_assets, RampErrors::ASSET_ALREADY_ADDED);

            // ipdate asset tracking info
            let tracked_asset_index:u8 = self.allowed_tokens_tracker.read() + 1;
            self.allowed_tokens_tracker.write(tracked_asset_index);

            //update the asset in the tracker
            self.allowed_tokens_map.write(tracked_asset_index, asset);

            self.allowed_tokens.write(asset, true);

            self.emit(AssetAllowedAdded {
                asset
            });

        }

        /// 
        /// @dev Remove an asset from the allowed assets list.
        /// @param asset The address of the asset to be removed.
        /// @notice This function can only be called by the owner of the contract.
        /// 
        fn remove_allowed_asset(ref self: ContractState, asset: ContractAddress) {
            self.ownable.assert_only_owner();
            // Implementation for removing an asset
            assert(self.is_asset_allowed(asset), RampErrors::INVALID_ASSET);

            self.allowed_tokens.write(asset, false);

            let assets = self.allowed_tokens_tracker.read();

            // remove the asset from the tracking map and update the tracking
            for i in 1..assets {
                if self.allowed_tokens_map.read(i) == asset {
                    let last_asset =  self.allowed_tokens_map.read(assets);
                    self.allowed_tokens_map.write(i, last_asset);
                    self.allowed_tokens_tracker.write(assets - 1);
                    break;
                }
            }
            self.emit(AssetAllowedRemoved {
                asset
            });

        }

        fn on_ramp_deposit(
            ref self: ContractState,
            asset: ContractAddress,
            amount: u256,
            sender: ContractAddress
        ) {
            assert(self.is_asset_allowed(asset), RampErrors::INVALID_ASSET);

            let token = IERC20Dispatcher { contract_address: asset };

            assert(
                token.transfer_from(sender, get_contract_address(), amount),
                RampErrors::TOKEN_DEPOSIT_FAILED
            );

            self.emit(
                RampDeposit {
                    asset: asset,
                    amount: amount,
                    sender: get_caller_address()
                }
            );
        }

        fn off_ramp_withdraw(
            ref self: ContractState,
            asset: ContractAddress,
            amount: u256,
            recipient: ContractAddress
        ) {
            assert(self.is_asset_allowed(asset), RampErrors::INVALID_ASSET);
            self.ownable.assert_only_owner();
            let token = IERC20Dispatcher { contract_address: asset };

            assert(
                token.transfer(recipient, amount),
                RampErrors::TOKEN_DEPOSIT_FAILED
            );

            self.emit(
                RampWithdraw {
                    asset: asset,
                    amount: amount,
                    recipient: recipient
                }
            );

        }

        fn is_asset_allowed(self: @ContractState, asset: ContractAddress) -> bool{
            self.allowed_tokens.read(asset)
        }

        fn get_allowed_assets(self: @ContractState) -> Array<ContractAddress> {
            let assets = self.allowed_tokens_tracker.read();

            let mut assets_array: Array<ContractAddress> = ArrayTrait::new();

            for i in 1..assets {
                assets_array.append(self.allowed_tokens_map.read(i));
            }

            assets_array
        }

        fn set_new_vault(ref self: ContractState, vault: ContractAddress) {
            self.ownable.assert_only_owner();

            let old_vault = self.vault_address.read();
            assert(old_vault != vault, RampErrors::SAME_VAULT_ADDRESS);

            self.vault_address.write(vault);

            self.emit(
                VaultChanged {
                    old_vault: old_vault,
                    new_vault: vault
                }
            );
        }

        fn set_fee(ref self: ContractState, asset: ContractAddress, fee: u256) {
            self.ownable.assert_only_owner();
            let old_fee = self.project_revenue_per_asset.read(asset);
            assert(
                old_fee != fee,
                RampErrors::SAME_FEE_AMOUNT
            );
            self.project_revenue_per_asset.write(asset, fee);

            self.emit(
                AssetFeeChanged {
                    asset: asset,
                    old_fee: old_fee,
                    new_fee: fee
                }
            );
        }

    }
    //
    // Upgradeable
    //
    
    #[abi(embed_v0)]
    impl UpgradeableImpl of IUpgradeable<ContractState> {
        fn upgrade(ref self: ContractState, new_class_hash: ClassHash) {
            self.ownable.assert_only_owner();
            self.upgradeable.upgrade(new_class_hash);
        }
    }
}
